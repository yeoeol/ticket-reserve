plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    group = 'ticket'
    version = '0.0.1'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
    }
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    dependencies {
        implementation 'ch.qos.logback:logback-classic'

        implementation("org.springframework.boot:spring-boot-starter-actuator")

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0" // 버전은 Boot와 맞춰야 함
        }
    }

    if (project.path.startsWith(':service') || project.path.startsWith(':platform')) {
        apply plugin: 'org.springframework.boot'

        bootJar { enabled = true }
        jar { enabled = false }
    } else {
        apply plugin: 'org.springframework.boot'

        bootJar { enabled = false }
        jar { enabled = true }
    }
}

